datatype Byte<1>
@type_checker "Byte"
@java_type "byte";

datatype Integer<2>
@type_checker "Integer"
@java_type "short";

protocol Default;

thing fragment Msgs {
	message m1(a : Byte, b : Byte, c : Byte, d : Byte, e : Byte)
	message m2(a : Byte, b : Byte, c : Byte)
	message m3(a : Byte)
	
	function rnd() : Byte do 
		return 0xBB
	end
}

thing App includes Msgs {
	
	property id : Byte

	provided port app {
		receives m1, m2
		sends m3
	}

	statechart init WaitForM1 {
		state WaitForM1 {
			transition -> WaitForM2
			event e : app?m1
			action do
				id = e.a
				println "got app?m1(", e.a, ", ", e.b, ", ", e.c, ", ", e.d, ", ", e.e, ")"
			end
		}
			
		state WaitForM2 {
			transition -> SendAck
			event e : app?m2
			action println "got app?m2(", e.a, ", ", e.b, ", ", e.c, ")"
		}
			
		state SendAck {
			on entry do
				println "sending app!m3(", id, ")"
				app!m3(id)
			end
				
			transition -> WaitForM1
		}
	}

}

thing Client includes Msgs {
	
	property counter : Integer = 0
	
	property _a : Byte
	property _b : Byte
	property _c : Byte
	property _d : Byte
	property _e : Byte
	
	required port app {
		sends m1, m2
		receives m3
	}

	statechart init RUN {
		on entry do
			_a = rnd()
			_b = rnd()
		end
		
		state RUN {
			on entry do
				_c = rnd()
				_d = rnd()
				_e = rnd()
				println "sending app!m1(", _a, ", ", _b, ", ", _c, ", ", _d, ", ", _e, ")"
				app!m1(_a, _b, _c, _d, _e)
				println "sending app!m2(", _a, ", ", _b, ", ", _c, ")"
				app!m2(_a, _b, _c)
				counter++
			end
			
			transition -> STOP
			guard counter == 100
			
			transition -> RUN
			event e : app?m3
			action do
				println "got app?m3(", e.a, ")"
				if (e.a == _a) do
					println "success!"
				end
			end
		}
		
		final state STOP {}
	}

}

configuration test {
	instance app : App
	instance client1 : Client

	connector client1.app => app.app
}
